
cmake_minimum_required(VERSION 3.0)

# Options
# =======

project(GMatElastoPlasticFiniteStrainSimo)

option(BUILD_TESTS "${PROJECT_NAME} Build tests" OFF)

# Internals
# =========

set(GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/include)

# Version
# =======

file(
    STRINGS "${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/config.h" GMatElastoPlasticFiniteStrainSimo_version_defines
    REGEX "#define GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_(MAJOR|MINOR|PATCH)")

foreach(ver ${GMatElastoPlasticFiniteStrainSimo_version_defines})
    if(ver MATCHES "#define GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_(MAJOR|MINOR|PATCH) +([^ ]+)$")
        set(GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_${CMAKE_MATCH_1} "${CMAKE_MATCH_2}" CACHE INTERNAL "")
    endif()
endforeach()

set(GMATELASTOPLASTICFINITESTRAINSIMO_VERSION
    ${GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_MAJOR}.${GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_MINOR}.${GMATELASTOPLASTICFINITESTRAINSIMO_VERSION_PATCH})

message(STATUS "Building GMatElastoPlasticFiniteStrainSimo v${GMATELASTOPLASTICFINITESTRAINSIMO_VERSION}")

# Build
# =====

set(GMATELASTOPLASTICFINITESTRAINSIMO_HEADERS
    ${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/Cartesian3d.h
    ${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/Cartesian3d.hpp
    ${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/Cartesian3d_Elastic.hpp
    ${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/Cartesian3d_Matrix.hpp
    ${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}/GMatElastoPlasticFiniteStrainSimo/config.h)

add_library(${PROJECT_NAME} INTERFACE)

target_include_directories(
    ${PROJECT_NAME} INTERFACE
    $<BUILD_INTERFACE:${GMATELASTOPLASTICFINITESTRAINSIMO_INCLUDE_DIR}>
    $<INSTALL_INTERFACE:include>)

# Installation
# ============

include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

install(
    TARGETS ${PROJECT_NAME}
    EXPORT ${PROJECT_NAME}-targets)

export(
    EXPORT ${PROJECT_NAME}-targets
    FILE "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Targets.cmake")

install(
    FILES ${GMATELASTOPLASTICFINITESTRAINSIMO_HEADERS}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME})

set(GMATELASTOPLASTICFINITESTRAINSIMO_CMAKECONFIG_INSTALL_DIR
    "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}" CACHE
    STRING "install path for ${PROJECT_NAME}Config.cmake")

configure_package_config_file(
    "GMatElastoPlasticFiniteStrainSimoConfig.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
    INSTALL_DESTINATION ${GMATELASTOPLASTICFINITESTRAINSIMO_CMAKECONFIG_INSTALL_DIR})

set(_GMATELASTOPLASTICFINITESTRAINSIMO_CMAKE_SIZEOF_VOID_P ${CMAKE_SIZEOF_VOID_P})

unset(CMAKE_SIZEOF_VOID_P)

write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
    VERSION ${GMATELASTOPLASTICFINITESTRAINSIMO_VERSION}
    COMPATIBILITY AnyNewerVersion)

set(CMAKE_SIZEOF_VOID_P ${_GMATELASTOPLASTICFINITESTRAINSIMO_CMAKE_SIZEOF_VOID_P})

install(
    FILES
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
    DESTINATION ${GMATELASTOPLASTICFINITESTRAINSIMO_CMAKECONFIG_INSTALL_DIR})

install(
    EXPORT ${PROJECT_NAME}-targets
    FILE "${PROJECT_NAME}Targets.cmake"
    DESTINATION ${GMATELASTOPLASTICFINITESTRAINSIMO_CMAKECONFIG_INSTALL_DIR})

configure_file(
    "GMatElastoPlasticFiniteStrainSimo.pc.in"
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}.pc"
     @ONLY)

install(
    FILES "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}.pc"
    DESTINATION "${CMAKE_INSTALL_LIBDIR}/pkgconfig/")

# Add builds
# ==========

if(BUILD_TESTS)
    add_subdirectory(test)
endif()
